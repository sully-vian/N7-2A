(* les deux types de flux utilisés: le flux à parser et le flux des solutions *)
(* (le fait de passer () à Make assure que ces deux types de flux seront différents et ne pourront donc pas être mélangés involontairement) *)
module Flux = Monadic_flux.Make ()
module Solution = Monadic_flux.Make ()

(* types des parsers généraux *)
type ('a, 'b) result = ('b * 'a Flux.t) Solution.t
type ('a, 'b) parser = 'a Flux.t -> ('a, 'b) result

(* interface des parsers: combinateurs de parsers et parsers simples *)
module type Parsing = sig
  val map : ('b -> 'c) -> ('a, 'b) parser -> ('a, 'c) parser
  val return : 'b -> ('a, 'b) parser
  val ( >>= ) : ('a, 'b) parser -> ('b -> ('a, 'c) parser) -> ('a, 'c) parser
  val zero : ('a, 'b) parser
  val ( ++ ) : ('a, 'b) parser -> ('a, 'b) parser -> ('a, 'b) parser
  val run : ('a, 'b) parser -> 'a Flux.t -> 'b Solution.t
  val pvide : ('a, unit) parser
  val ptest : ('a -> bool) -> ('a, 'a) parser
  val ( *> ) : ('a, 'b) parser -> ('a, 'c) parser -> ('a, 'b * 'c) parser
end

(* implantation des parsers, comme vu en TD. On utilise les opérations *)
(* du module Flux et du module Solution                                *)
module Parser : Parsing = struct
  let map fmap parse f = Solution.map (fun (b, f') -> fmap b, f') (parse f)
  let return b f = Solution.return (b, f)
  let ( >>= ) parse dep_parse f = Solution.(parse f >>= fun (b, f') -> dep_parse b f')
  let zero f = Solution.zero
  let ( ++ ) parse1 parse2 f = Solution.(parse1 f ++ parse2 f)

  let run parse f =
    Solution.(map fst (filter (fun (b, f') -> Flux.uncons f' = None) (parse f)))
  ;;

  let pvide f =
    match Flux.uncons f with
    | None -> Solution.return ((), f)
    | Some _ -> Solution.zero
  ;;

  let ptest p f =
    match Flux.uncons f with
    | None -> Solution.zero
    | Some (t, q) -> if p t then Solution.return (t, q) else Solution.zero
  ;;

  let ( *> ) parse1 parse2 f =
    Solution.(
      parse1 f >>= fun (b, f') -> parse2 f' >>= fun (c, f'') -> return ((b, c), f''))
  ;;
end
